// SPDX-License-Identifier: MIT
pragma solidity ^0.8.30;

import { OFTAdapter } from "@layerzerolabs/oft-evm/contracts/OFTAdapter.sol";
import { OAppSupplyLiquidityUSDT } from "./OAppSupplyLiquidityUSDT.sol";
import { SendParam } from "@layerzerolabs/oft-evm/contracts/interfaces/IOFT.sol";
import { OptionsBuilder } from "@layerzerolabs/oapp-evm/contracts/oapp/libs/OptionsBuilder.sol";
import { MessagingFee } from "@layerzerolabs/oapp-evm/contracts/oapp/OApp.sol";
import { IERC20 } from "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import { SafeERC20 } from "@openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol";
import { ReentrancyGuard } from "@openzeppelin/contracts/utils/ReentrancyGuard.sol";

/**
 * @title OAppAdapter
 * @notice Adapter contract for coordinating cross-chain token bridging and liquidity operations
 * @dev Combines OFT token transfers with OApp messaging to enable complex cross-chain operations
 */
contract OAppAdapter is ReentrancyGuard {
    /// @notice Emitted when a bridge operation is initiated
    /// @param _oapp Address of the OApp contract handling the message
    /// @param _oft Address of the OFT adapter for token transfer
    /// @param _lendingPoolDst Destination lending pool address
    /// @param _tokenSrc Source token address
    /// @param _tokenDst Destination token address
    /// @param _toAddress Recipient address
    /// @param _dstEid Destination endpoint ID
    /// @param _amount Amount being bridged
    /// @param _oftFee Fee for OFT token transfer
    /// @param _oappFee Fee for OApp messaging
    event sendBridgeOApp(
        address _oapp,
        address _oft,
        address _lendingPoolDst,
        address _tokenSrc,
        address _tokenDst,
        address _toAddress,
        uint32 _dstEid,
        uint256 _amount,
        uint256 _oftFee,
        uint256 _oappFee
    );

    using SafeERC20 for IERC20;
    using OptionsBuilder for bytes;

    /**
     * @notice Initiates a cross-chain bridge operation combining token transfer and messaging
     * @param _oapp Address of the OApp contract for messaging
     * @param _oft Address of the OFT adapter for token transfer
     * @param _lendingPoolDst Destination lending pool address
     * @param _tokenSrc Source chain token address
     * @param _tokenDst Destination chain token address
     * @param _toAddress Recipient address on destination chain
     * @param _dstEid Destination endpoint ID
     * @param _amount Amount of tokens to bridge
     * @param _oftFee Native fee for OFT transfer
     * @param _oappFee Native fee for OApp messaging
     * @dev Combines OFT send and OApp message in a single atomic operation
     */
    function sendBridge(
        address _oapp,
        address _oft,
        address _lendingPoolDst,
        address _tokenSrc,
        address _tokenDst,
        address _toAddress,
        uint32 _dstEid,
        uint256 _amount,
        uint256 _oftFee,
        uint256 _oappFee
    )
        external
        payable
        nonReentrant
    {
        (SendParam memory sendParam, MessagingFee memory fee) = _utils(_dstEid, _toAddress, _amount, _oft);
        OFTAdapter(_oft).send{ value: _oftFee }(sendParam, fee, _toAddress);
        OAppSupplyLiquidityUSDT(_oapp).sendString{ value: _oappFee }(_dstEid, _lendingPoolDst, _toAddress, _tokenDst, _amount, _oappFee, "");
        emit sendBridgeOApp(_oapp, _oft, _lendingPoolDst, _tokenSrc, _tokenDst, _toAddress, _dstEid, _amount, _oftFee, _oappFee);
    }

    /**
     * @notice Internal utility to prepare OFT send parameters
     * @param _dstEid Destination endpoint ID
     * @param _toAddress Recipient address
     * @param _amount Amount to send
     * @param _oft OFT adapter address
     * @return sendParam Prepared send parameters
     * @return fee Calculated messaging fee
     * @dev Configures LayerZero options with 65000 gas for execution
     */
    function _utils(
        uint32 _dstEid,
        address _toAddress,
        uint256 _amount,
        address _oft
    )
        internal
        view
        returns (SendParam memory sendParam, MessagingFee memory fee)
    {
        bytes memory extraOptions = OptionsBuilder.newOptions().addExecutorLzReceiveOption(65000, 0);

        sendParam = SendParam({
            dstEid: _dstEid,
            to: _addressToBytes32(_toAddress),
            amountLD: _amount,
            minAmountLD: _amount,
            extraOptions: extraOptions,
            composeMsg: "",
            oftCmd: ""
        });

        fee = OFTAdapter(_oft).quoteSend(sendParam, false);
    }

    /**
     * @notice Converts an address to bytes32 format for LayerZero
     * @param _addr Address to convert
     * @return bytes32 representation of the address
     */
    function _addressToBytes32(address _addr) internal pure returns (bytes32) {
        return bytes32(uint256(uint160(_addr)));
    }
}
